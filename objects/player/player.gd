extends Actor
class_name Player

var PAUSE_MENU : PackedScene
const DEATH_THRESHOLD := -20.0
const IFRAME_TIME := 3.0
const PAUSE_DELAY := 0.25

## Local-only layer (camera + HUD). Only instantiated for the local/authority
## player -- see TGM-39. Never spawned for a remote player's body, so a
## player controlled by another peer never creates a second camera or HUD.
const PLAYER_CAMERA_SCENE: PackedScene = preload("res://objects/player/player_camera.tscn")
const PLAYER_HUD_SCENE: PackedScene = preload("res://objects/player/player_hud.tscn")
const PLAYER_CAMERA_Y_OFFSET := 1.06085

## Object states
enum PlayerState {
	WALK,
	STOPPED,
	SAD,
	CHASE,
	PUSH,
}
@export var state := PlayerState.STOPPED:
	set(x):
		await NodeGlobals.until_ready(self)
		# On a remote body, Push has nothing to push: PushState._enter() needs a
		# local PushableComponent that only the owning peer assigned. Record the
		# value and let the replicated animation show the pose.
		if x == PlayerState.PUSH and not is_multiplayer_authority():
			state = x
			return
		var state_name: StringName = PlayerStateToName[x]
		if state_name != controller.current_state_name:
			controller.request(state_name)
		if controller.current_state_name == state_name:
			state = x

static var PlayerStateToName: Dictionary[PlayerState, StringName] = {
	PlayerState.WALK: &"Walk",
	PlayerState.STOPPED: &"Stopped",
	PlayerState.SAD: &"Sad",
	PlayerState.CHASE: &"Chase",
	PlayerState.PUSH: &"Push",
}
static var PlayerStateNameToState := ToonUtils.reverse_dictionary(PlayerStateToName)

## Exports
@export var stats: PlayerStats:
	set(x):
		stats = x
		print('stats set')
@export var head_node: Node3D
@export var partners: Array[CharacterBody3D] = []

## Child References
@onready var controller: FiniteStateMachine3D = %Controller

## Local-only (camera/HUD) child references. These are null on any instance
## that is not the local multiplayer authority, since the local layer is
## never instantiated for one -- guard every use with a null/is_instance_valid
## check rather than assuming these are present.
@onready var camera: PlayerCamera = get_node_or_null("%PlayerCamera")
@onready var camera_dist: float:
	set(x):
		if not camera:
			return
		var cam_tween := create_tween()
		cam_tween.tween_property(camera, 'spring_length', x, 0.1)
	get:
		return camera.spring_length if camera else 0.0

var control_style: bool:
	get: return SaveFileService.settings_file.control_style

var run_speed := 8.0
var gravity := 16.0
var ignore_battles := false

@onready var gui: Control = get_node_or_null("%GUI")
@onready var laff_meter := get_node_or_null("%LaffMeter")
@onready var bean_jar := get_node_or_null("%BeanJar")
@onready var toon: Toon = $Toon
@onready var character: PlayerCharacter:
	get:
		if not is_node_ready():
			await ready
		if not stats:
			return null
		return stats.character
@onready var item_node := $Items
@onready var boost_queue: BoostQueue = get_node_or_null("%BoostTextQueue")

@onready var game_timer: Control = get_node_or_null("%GameTimer")
var game_timer_tick := false:
	set(x):
		if not lock_game_timer:
			game_timer_tick = x
			if game_timer:
				if x: game_timer.set_timer_color(Color.WHITE)
				else: game_timer.set_timer_color(Color.YELLOW)
var lock_game_timer := false
@onready var active_item_ui: Control = get_node_or_null("%ActiveItemUI")
@onready var item_descriptions: CanvasLayer = get_node_or_null("%ItemDescriptions")

var last_damage_source: String = "Something"

var animator: AnimationPlayer
var pause_delay := 0.0

## Item-Manipulated Values
var see_descriptions: bool = false:
	set(x):
		see_descriptions = x
		if item_descriptions:
			item_descriptions.visible = x
var see_anomalies := 0
var random_cog_heals := 0
var custom_gag_order := 0
var less_shop_items := 0
var better_battle_rewards := 0
var no_negative_anomalies := 0
var throw_heals := 1
var trap_needs_lure := 1
var inverted_sound_damage := 0
var obscured_anomalies := 0
## Damage immunity from light-based obstacles, such as spotlights and goon beams.
var immune_to_light_damage := 0
## Damage immunity from stompers and other crush-based obstacles
var immune_to_crush_damage := 0
## Unique for Prof. Pete
var gags_cost_beans := 0
## Used for Oldman
var revives_are_hp := 0
## Used in battle to override Gag prices
var free_gags : Array[ToonAttack] = []
## For modders who hate their players
var use_accuracy := 0
## Self explanatory
var cogs_always_hit := 0
var stranger_guaranteed := false
var obscured_laff: bool:
	get: return laff_meter.obscured if laff_meter else false
	set(x):
		if laff_meter: laff_meter.obscured = x
var alt_gag_hotswap := false

var laff_lock_enabled := false:
	set(x):
		laff_lock_enabled = x
		if is_instance_valid(laff_meter):
			laff_meter.lock_enabled = x

var laff_lock := false:
	set(x):
		laff_lock = x
		if is_instance_valid(laff_meter):
			laff_meter.locked = x

signal s_fell_out_of_world(player: Player)
signal s_died
signal s_dying
signal s_jumped
signal s_stats_connected(stats: PlayerStats)
signal s_hurt_realtime(damage: int)

#region Network replication (TGM-37)

const NET_SYNC_NAME := &"NetSync"

## Replicated mirror of the current toon animation. The owning peer's value is
## read from the animator; remote peers play whatever arrives. Empty strings
## (a one-shot animation that finished) are ignored.
var net_animation: String:
	get:
		return animator.current_animation if animator else ""
	set(x):
		if is_multiplayer_authority() or x.is_empty() or not animator:
			return
		set_animation(x)

## Adds the MultiplayerSynchronizer that replicates this body from its owner.
## Call before set_multiplayer_authority() and before the node enters the tree
## (done by PlayerSpawner). Decision on TGM-26: a synchronizer, not @rpc.
func setup_network_sync() -> void:
	if has_node(NodePath(NET_SYNC_NAME)):
		return
	var config := SceneReplicationConfig.new()
	# Continuous motion: sent every sync tick, unreliable.
	for path in [^".:position", ^".:velocity", ^"Toon:rotation"]:
		config.add_property(path)
		config.property_set_replication_mode(path, SceneReplicationConfig.REPLICATION_MODE_ALWAYS)
	# Discrete values: only sent when they change.
	for path in [^".:state", ^".:net_animation"]:
		config.add_property(path)
		config.property_set_replication_mode(path, SceneReplicationConfig.REPLICATION_MODE_ON_CHANGE)
	var sync := MultiplayerSynchronizer.new()
	sync.name = NET_SYNC_NAME
	sync.replication_config = config
	add_child(sync)

#endregion

func _init() -> void:
	GameLoader.queue_into(GameLoader.Phase.GAMEPLAY, self, {
		'PAUSE_MENU': "res://objects/pause_menu/pause_menu.tscn",
	})

## _enter_tree() fires on every tree re-entry, not just the first -- including
## every player.reparent(...) call (battles, several cog-attack/boss movies,
## the golf cart). Godot does NOT re-run _ready()/@onready on a reparent, only
## _enter_tree()/_exit_tree(), so without this guard each reparent would stack
## another camera + HUD on top of the one from the original _ready().
var _local_layer_instantiated := false

func _enter_tree() -> void:
	# Local-only layer must exist before @onready resolves it below, so it's
	# built here rather than in _ready(). Multiplayer authority has to be
	# assigned before a node enters the tree (see architecture.md), so this
	# reads correctly for a remote-owned body spawned under a
	# MultiplayerSpawner (TGM-37) as well as for the offline/single-player
	# default, where every node is its own authority.
	if is_multiplayer_authority() and not _local_layer_instantiated:
		_instantiate_local_layer()

func _instantiate_local_layer() -> void:
	_local_layer_instantiated = true

	var camera_node: PlayerCamera = PLAYER_CAMERA_SCENE.instantiate()
	camera_node.process_physics_priority = -100
	camera_node.position.y = PLAYER_CAMERA_Y_OFFSET
	camera_node.unique_name_in_owner = true
	add_child(camera_node)
	camera_node.owner = self

	var gui_node: Control = PLAYER_HUD_SCENE.instantiate()
	add_child(gui_node)
	gui_node.owner = self
	for child in gui_node.get_children():
		child.owner = self

func _ready() -> void:
	state = state
	
	# Make player globally accessible -- only the local player should ever be
	# "the" player (TGM-24 registry-alongside strategy). Gating on
	# is_multiplayer_authority() keeps Util.player meaning "the local player"
	# once remote-owned bodies exist.
	if is_multiplayer_authority():
		Util.player = self
	
	# Construct the toon from the character DNA
	toon.construct_toon(character.dna)
	print('toon constructed')
	animator = toon.body.animator
	
	# The local-only layer (camera + HUD) doesn't exist on a remote body, so
	# camera-dependent initial-orientation setup only runs for the local
	# player. A remote body's initial facing is a replication concern for
	# TGM-37, not this ticket.
	if is_multiplayer_authority():
		laff_meter.set_meter(character.dna)
		
		# Correct rotation
		camera.rotate_y(rotation.y)
		toon.rotation.y = camera.rotation.y
		rotation = Vector3(0, 0, 0)
	
	# Set to the neutral anim
	set_animation('neutral')
	
	# Hook up stats
	connect_stats()

func _physics_process(_delta: float) -> void:
	# Fall-out-of-world and the laff lock hotkey are the owning peer's business.
	if not is_multiplayer_authority():
		return
	# Emit signal when player is under death threshold
	if global_position.y < DEATH_THRESHOLD:
		s_fell_out_of_world.emit(self)

	# Temp
	if Input.is_action_just_pressed('ui_focus_next') and laff_lock_enabled:
		laff_lock = not laff_lock

func _process(delta: float) -> void:
	# A remote body must not open the local pause menu or toggle the local HUD.
	if not is_multiplayer_authority():
		return
	# Hide GUI
	if gui and Input.is_action_just_pressed('hide_gui'):
		gui.set_visible(not gui.visible)
	
	# Pause Logic
	if not controller.current_state.accepts_interaction():
		return
	if pause_delay < PAUSE_DELAY:
		pause_delay += delta
		return
	if Input.is_action_just_pressed("pause"):
		pause_delay = 0.0
		Input.set_mouse_mode(Input.MOUSE_MODE_VISIBLE)
		get_tree().get_root().add_child(PAUSE_MENU.instantiate())
	
	while absf(toon.rotation_degrees.y) > 360.0:
		toon.rotation_degrees.y -= 360.0 * signf(toon.rotation_degrees.y)

func move_to(new_pos: Vector3, spd: float = 0.0, override_anim := "") -> Tween:
	if spd == 0.0:
		spd = controller.get_state(&"Walk").run_speed
	# Stop player if not already
	if controller.current_state.accepts_interaction():
		state = PlayerState.STOPPED
	# Calculate move time
	var time = new_pos.distance_to(global_position) / spd
	# Set movement anim
	if time > 0.5:
		set_animation('run')
	else:
		set_animation('walk')
	if override_anim != "":
		set_animation(override_anim)
	# Look at new position
	face_position(new_pos)
	# Use tween to move
	var move_tween = create_tween()
	move_tween.tween_property(self, 'global_position', new_pos, time)
	move_tween.finished.connect(move_tween_finished.bind(move_tween))
	return move_tween

func move_tween_finished(tween: Tween):
	set_animation('neutral')
	tween.kill()

func face_position(pos: Vector3):
	toon.look_at(Vector3(pos.x, global_position.y, pos.z), Vector3.UP, true)

func turn_to_position(pos: Vector3, time: float):
	set_animation('walk')
	var toon_scale: Vector3 = toon.scale
	var cur_rot: Vector3 = toon.global_rotation
	face_position(pos)
	var new_rot: Vector3 = toon.global_rotation
	toon.global_rotation = cur_rot
	
	var turn_tween := create_tween()
	turn_tween.set_parallel(true)
	turn_tween.tween_method(toon_lerp_angle.bind(cur_rot.y, new_rot.y, toon_scale), 0.0, 1.0, time)
	await turn_tween.finished
	turn_tween.kill()
	set_animation('neutral')

func toon_lerp_angle(weight: float, start_angle: float, end_angle: float, toon_scale: Vector3) -> void:
	toon.rotation.y = lerp_angle(start_angle, end_angle, weight)
	toon.set_scale(toon_scale)

func set_animation(anim: String):
	if state == PlayerState.SAD and not anim == 'lose': return
	if not get_animation() == anim:
		toon.set_animation(anim)

func get_animation() -> String:
	return animator.current_animation

func lose():
	if state == PlayerState.SAD:
		# Thog don't care if we're already in the sad state
		return
	
	s_dying.emit()
	SaveFileService.on_game_over()
	SaveFileService.progress_file.on_player_died()
	state = PlayerState.SAD
	Util.stuck_lock = false
	set_animation('lose')
	await Task.delay(2.0)
	AudioManager.play_sound(load('res://audio/sfx/toon/ENC_Lose.ogg'))
	await Task.delay(2.0)
	var shrink_tween := create_tween()
	shrink_tween.tween_property(toon, 'scale', Vector3(.01, .01, .01), 2.0)
	await shrink_tween.finished
	shrink_tween.kill()
	SaveFileService.progress_file.deaths += 1
	s_died.emit()

func start_pushing(push_object: PushableComponent):
	var push_state := controller.states[PlayerStateToName[PlayerState.PUSH]]
	push_state.push_object = push_object
	state = PlayerState.PUSH
	
func stop_pushing(next_state = PlayerState.WALK):
	if state == PlayerState.PUSH:
		state = next_state

func speak(phrase: String) -> void:
	toon.speak(phrase)

func teleport_in(set_to_walk := false) -> void:
	state = PlayerState.STOPPED
	await toon.teleport_in()
	if set_to_walk:
		state = PlayerState.WALK

func teleport_out() -> void:
	state = PlayerState.STOPPED
	await toon.teleport_out()

func fall_in(set_to_walk := false) -> void:
	state = PlayerState.STOPPED
	toon.position.y = 50.0
	toon.set_animation('slip-forward')
	var fall_tween := create_tween()
	fall_tween.tween_property(toon, 'position:y', 0.0, 0.5)
	await fall_tween.finished
	AudioManager.play_sound(load("res://audio/sfx/toon/MG_cannon_hit_dirt.ogg"))
	await animator.animation_finished
	fall_tween.kill()
	if set_to_walk:
		state = PlayerState.WALK

func reset_stats() -> void:
	var newstats := PlayerStats.new()
	newstats.character = stats.character
	newstats.quests = stats.quests
	stats = newstats
	# Delete items if they exist
	if item_node:
		for item in item_node.get_children():
			item.queue_free()
	# Delete any accessory items
	if toon:
		var nodes := [toon.hat_node, toon.glasses_node, toon.backpack_node]
		for node in nodes:
			for child in node.get_children():
				child.queue_free()
	
	if newstats.character:
		newstats.set_loadout(newstats.character.gag_loadout)
		newstats.first_time_setup()
		newstats.character.character_setup(self)
	if laff_meter:
		connect_stats()
	

func connect_stats() -> void:
	# Update laff meter on hp/max hp update
	if laff_meter:
		laff_meter.max_laff = stats.max_hp
		laff_meter.laff = stats.hp
		laff_meter.extra_lives = stats.extra_lives
		laff_meter.lock_enabled = laff_lock_enabled
		stats.hp_changed.connect(laff_meter.set_laff)
		stats.max_hp_changed.connect(laff_meter.set_max_laff)
		stats.s_extra_lives_changed.connect(func(x: int): laff_meter.extra_lives = x)
	if bean_jar:
		bean_jar.bean_count = stats.money
		stats.s_money_changed.connect(func(x: int): bean_jar.bean_count = x)
		stats.s_gained_money.connect(bean_jar.scale_pop)
	stats.hp_changed.connect(check_hp)
	# Regenerate points at end of round
	if not BattleService.s_round_ended.is_connected(stats.on_round_end):
		BattleService.s_round_ended.connect(stats.on_round_end)
	if not BattleService.s_battle_started.is_connected(stats.on_battle_started):
		BattleService.s_battle_started.connect(stats.on_battle_started)
	if active_item_ui:
		stats.s_active_item_changed.connect(func(newitem): active_item_ui.item = newitem)
	stats.current_active_item = stats.current_active_item
	if stats.current_active_item and not stats.current_active_item.node:
		stats.current_active_item.apply_item(self)
	s_stats_connected.emit(stats)

var prev_hp := -1
func check_hp(hp : int) -> void:
	if prev_hp > -1 and laff_lock and hp > prev_hp:
		stats.hp = prev_hp
	
	if hp == 0 and not BattleService.ongoing_battle:
		lose()
	prev_hp = stats.hp

func quick_heal(amount: int, allow_iframes := true) -> void:
	var pre_hp := stats.hp
	# Apply healing effectiveness if we have it
	if amount > 0 and not is_equal_approx(stats.healing_effectiveness, 1.0):
		amount = roundi(amount * stats.healing_effectiveness)

	stats.hp += amount
	var diff := stats.hp - pre_hp
	if diff == 0:
		return
	if sign(diff) == -1:
		if controller.current_state.accepts_interaction() and allow_iframes:
			do_invincibility_frames()
			s_hurt_realtime.emit(diff)
		Util.do_3d_text(self,str(diff))
	else:
		Util.do_3d_text(self, "+" + str(diff), Color.GREEN, Color.DARK_GREEN)


func recenter_camera(instant := true) -> void:
	if not camera:
		return
	if instant:
		camera.rotation = Vector3.ZERO
		camera.rotation_degrees.y = toon.rotation_degrees.y + 180.0
	else:
		camera.recentering = true


func do_invincibility_frames(time := IFRAME_TIME) -> void:
	set_collision_mask_value(Globals.HAZARD_COLLISION_LAYER, false)
	set_collision_layer_value(Globals.HAZARD_COLLISION_LAYER, false)
	await do_iframe_tween(time).finished
	set_collision_layer_value(Globals.HAZARD_COLLISION_LAYER, true)
	set_collision_mask_value(Globals.HAZARD_COLLISION_LAYER, true)

var iframe_tween: Tween
var INVINCIBLE_COLOR := Color(0.542, 0.691, 1.0, 1.0)

func do_iframe_tween(time := IFRAME_TIME) -> Tween:
	if iframe_tween:
		iframe_tween.kill()
	iframe_tween = create_tween()

	var delay := 0.9
	var delay_dec := 0.15 * (IFRAME_TIME / time)
	var delay_minimum := 0.1
	var blink_time := 0.0
	var fade_strength := 0.4

	toon.color_overlay_mat.set_color(INVINCIBLE_COLOR)
	while delay > delay_minimum:
		iframe_tween.tween_callback(toon.color_overlay_mat.fade_in.bind(toon, INVINCIBLE_COLOR, delay / 2.0, fade_strength))
		iframe_tween.tween_interval(delay / 2.0)
		iframe_tween.tween_callback(toon.color_overlay_mat.fade_out.bind(toon, INVINCIBLE_COLOR, delay / 2.0))
		iframe_tween.tween_interval(delay / 2.0)
		blink_time += delay
		delay -= delay_dec

	delay = delay_minimum
	while blink_time < time:
		iframe_tween.tween_callback(toon.color_overlay_mat.fade_in.bind(toon, INVINCIBLE_COLOR, delay / 2.0, fade_strength))
		iframe_tween.tween_interval(delay / 2.0)
		iframe_tween.tween_callback(toon.color_overlay_mat.fade_out.bind(toon, INVINCIBLE_COLOR, delay / 2.0))
		iframe_tween.tween_interval(delay / 2.0)
		blink_time += delay

	iframe_tween.tween_callback(toon.legs.show)
	return iframe_tween

func is_invincible() -> bool:
	return (iframe_tween and iframe_tween.is_running())

func swap_toon_visibility() -> void:
	toon.legs.visible = not toon.legs.visible

func update_accessories() -> void:
	# Remove all current accessories
	# Using free() even though it's scary
	for node in [toon.hat_node, toon.glasses_node, toon.backpack_node]:
		for child in node.get_children(): child.free()
	toon.legs.set_shoes(ToonLegs.ShoeType.NONE)
	
	var hat: ItemAccessory
	var glasses: ItemAccessory
	var backpack: ItemAccessory
	var shoes: ItemShoe
	for item: Item in stats.items:
		if item is ItemAccessory:
			match item.slot:
				Item.ItemSlot.HAT: hat = item
				Item.ItemSlot.GLASSES: glasses = item
				Item.ItemSlot.BACKPACK: backpack = item
		elif item is ItemShoe:
			shoes = item
	if hat: 
		hat.place_accessory(toon)
	
	if glasses: glasses.place_accessory(toon)
	
	if backpack: backpack.place_accessory(toon)
	
	if shoes:
		toon.legs.set_shoes(shoes.shoe_type as ToonLegs.ShoeType, shoes.get_correct_texture(toon.toon_dna))
